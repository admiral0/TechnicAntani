buildscript {
    ext.kotlin_version = '0.12.213'
    ext.vertx_version = '3.0.0-milestone6'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'com.jfrog.bintray'

    version = currentVersion
    group = 'it.admiral0.technicantani'

    repositories {
        jcenter()
    }

    configurations {
        published
    }
    dependencies {
        testCompile 'junit:junit:4.7'
        testCompile "org.mockito:mockito-core:1.+"
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    // Add the sourceJars to non-extractor modules
    artifacts {
        published sourceJar
        published javadocJar
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }

    bintray {
        user = System.getenv("BINTRAY_USER")
        key = System.getenv("BINTRAY_API_KEY")
        configurations = ['published', 'archives']
        pkg {
            repo = 'maven'
            websiteUrl = 'https://github.com/admiral0/TechnicAntani'
            issueTrackerUrl = 'https://github.com/admiral0/TechnicAntani'
            vcsUrl = 'https://github.com/admiral0/TechnicAntani.git'
            licenses = ['GPL-3']
            version {
                name = currentVersion
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}